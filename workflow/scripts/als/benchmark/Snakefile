from absplice_scripts.utils.model_utils import *

import pandas as pd
df_anno = pd.read_csv(config['DROP']['sample_annotation'], sep='\t')
df_anno = df_anno[~df_anno['VCF_ID'].isna()]
samples = list(set(df_anno[df_anno['VCF_ID'].str.contains('CASE')]['VCF_ID']))


rule jackknife:
    input:
        df_benchmark = expand(OUTPUT_DIR_BENCHMARK + config_static['benchmark']['combined_benchmark']['dna'], 
                              vcf_id=wildcard_vcf_id, tissue='{tissue}', tissue_pred='{tissue_pred}'),
    params:
        samples = samples,
        outlier_column = 'outlier',
        model_dict = model_dict_dna,
        median_n_cutoff = config['filtering_params']['absplice']['median_n_cutoff'],
    output:
        auPRC = OUTPUT_DIR_BENCHMARK + config_static['benchmark']['jackknife_performance']['auPRC']['dna'],
        stats = OUTPUT_DIR_BENCHMARK + config_static['benchmark']['jackknife_performance']['stats']['dna'],
    script:
        "../../../scripts/common/benchmark/jacknife_samples.py"
        
        
rule tp_preds_among_total_preds:
    input:
        benchmark = expand(OUTPUT_DIR_BENCHMARK + config_static['benchmark']['combined_benchmark']['dna'], 
                           vcf_id=wildcard_vcf_id, tissue='{tissue}', tissue_pred='{tissue_pred}'),
    params:
        model_dict = model_dict_dna,
        model_cutoffs = model_cutoffs,
        median_n_cutoff = config['filtering_params']['absplice']['median_n_cutoff'],
        subset_samples = True,
    output:
        tp_among_all_preds_line = OUTPUT_DIR_BENCHMARK + config_static['benchmark']['tp_preds_among_total_preds']['dna']['line'],
        tp_among_all_preds_discrete_cutoff = OUTPUT_DIR_BENCHMARK + config_static['benchmark']['tp_preds_among_total_preds']['dna']['discrete_cutoff']
    notebook:
        "../../../scripts/common/benchmark/tp_preds_among_total_preds.py" 
        
        
# Proteomics      
rule proteomics_outliers_prepare:
    input:
        results = config['proteomics']['full_results'],
        gene_map = OUTPUT_DIR_JUNCTION_ANNO + config_static['junction_annotation']['gene_map'],
        sample_map = config['DROP']['sample_annotation'],
    params:
        key_assay = config['filtering_params']['count_table']['key_assay'],
        value_assay = config['filtering_params']['count_table']['value_assay'],
    output:
        results_annotated = OUTPUT_DIR_PROTEOMICS + config['proteomics']['full_results_annotated'],
    script:
        './proteomics_outliers_prepare.py'
        
        
rule proteomics_enrichment:
    input:
        proteomics = OUTPUT_DIR_PROTEOMICS + config['proteomics']['full_results_annotated'],
        benchmark = expand(OUTPUT_DIR_BENCHMARK + config_static['benchmark']['combined_benchmark']['dna'], 
                           vcf_id=wildcard_vcf_id, tissue='{tissue}', tissue_pred='{tissue_pred}'),
    params:
        model_cutoffs = model_cutoffs
    output:
        enrichment = OUTPUT_DIR_PROTEOMICS + config['proteomics']['enrichment'],
        true_preds = OUTPUT_DIR_PROTEOMICS + config['proteomics']['true_preds'],
    script:
        './proteomics_enrichment.py'
        
        
# ALS known genes
rule als_enrichment_als_genes:
    input:
        benchmark = expand(OUTPUT_DIR_BENCHMARK + config_static['benchmark']['combined_benchmark']['dna'], 
                           vcf_id=wildcard_vcf_id, tissue='{tissue}', tissue_pred='{tissue_pred}'),
        als_genes = config_precomputed['als_genes'],
        coding_genes = OUTPUT_DIR_JUNCTION_ANNO + config_static['junction_annotation']['coding_genes'],
    params:
        model_cutoffs = model_cutoffs,
        median_n_cutoff = config['filtering_params']['absplice']['median_n_cutoff'],
    output:
        enrichment_als_genes = OUTPUT_DIR_BENCHMARK + config['als_known_genes'],
    script:
        "./als_enrichment_als_genes.py"
        
    
rule all_benchmark_als:
    input:
        expand(rules.jackknife.output, # jacknife
               tissue='motor_neuron',
               tissue_pred='Brain' 
              ),
        expand(rules.jackknife.output,
               tissue='motor_neuron',
               tissue_pred='motor_neuron' 
              ),
        expand(rules.tp_preds_among_total_preds.output, # true positive predictions among total preds
               tissue='motor_neuron',
               tissue_pred='Brain' 
              ),
        expand(rules.tp_preds_among_total_preds.output,
               tissue='motor_neuron',
               tissue_pred='motor_neuron' 
              ),
        expand(rules.proteomics_enrichment.output, # proteomics
               tissue='motor_neuron',
               tissue_pred='Brain' 
              ),
        expand(rules.proteomics_enrichment.output,
               tissue='motor_neuron',
               tissue_pred='motor_neuron' 
              ),
        expand(rules.als_enrichment_als_genes.output, # known genes
               tissue='motor_neuron',
               tissue_pred='Brain' 
              ),
        expand(rules.als_enrichment_als_genes.output,
               tissue='motor_neuron',
               tissue_pred='motor_neuron' 
              )