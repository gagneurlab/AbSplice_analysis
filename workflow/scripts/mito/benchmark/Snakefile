from absplice_scripts.utils.model_utils import *

rule jackknife:
    input:
        df_benchmark = expand(OUTPUT_DIR_BENCHMARK + config_static['benchmark']['combined_benchmark']['dna'], 
                              vcf_id=wildcard_vcf_id, tissue='{tissue}', tissue_pred='{tissue_pred}'),
    params:
        samples = config['sampleIDs'],
        outlier_column = 'outlier',
        model_dict = model_dict_dna,
        median_n_cutoff = config['filtering_params']['absplice']['median_n_cutoff'],
    output:
        auPRC = OUTPUT_DIR_BENCHMARK + config_static['benchmark']['jackknife_performance']['auPRC']['dna'],
        stats = OUTPUT_DIR_BENCHMARK + config_static['benchmark']['jackknife_performance']['stats']['dna'],
    script:
        "../../../scripts/common/benchmark/jacknife_samples.py"
        
        
rule tp_preds_among_total_preds:
    input:
        benchmark = expand(OUTPUT_DIR_BENCHMARK + config_static['benchmark']['combined_benchmark']['dna'], 
                           vcf_id=wildcard_vcf_id, tissue='{tissue}', tissue_pred='{tissue_pred}'),
    params:
        model_dict = model_dict_dna,
        model_cutoffs = model_cutoffs,
        median_n_cutoff = config['filtering_params']['absplice']['median_n_cutoff'],
        subset_samples = False,
    output:
        tp_among_all_preds_line = OUTPUT_DIR_BENCHMARK + config_static['benchmark']['tp_preds_among_total_preds']['dna']['line'],
        tp_among_all_preds_discrete_cutoff = OUTPUT_DIR_BENCHMARK + config_static['benchmark']['tp_preds_among_total_preds']['dna']['discrete_cutoff']
    script:
        "../../../scripts/common/benchmark/tp_preds_among_total_preds.py" 
        
        
rule all_mito_benchmark:
    input:
        expand(rules.jackknife.output,
               tissue='Prokisch_Fibroblasts',
               tissue_pred='Cells_Cultured_fibroblasts' 
              ),
        expand(rules.jackknife.output,
               tissue='Prokisch_Fibroblasts',
               tissue_pred='Prokisch_Fibroblasts' 
              ),
        expand(rules.tp_preds_among_total_preds.output,
               tissue='Prokisch_Fibroblasts',
               tissue_pred='Cells_Cultured_fibroblasts' 
              ),
        expand(rules.tp_preds_among_total_preds.output,
               tissue='Prokisch_Fibroblasts',
               tissue_pred='Prokisch_Fibroblasts' 
              )